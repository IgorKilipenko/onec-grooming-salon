#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(_, __, ___)
    РаботаСДокументами.ЗаполнитьПолеАвторДокументаНаСервере(ЭтотОбъект);
КонецПроцедуры

Процедура ОбработкаПроведения(_, __)
    выполнитьВсеДвижения();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

#Область Движения
Процедура выполнитьВсеДвижения()
    Движения.УчетЗатрат.Записывать = Истина;
    Движения.Хозрасчетный.Записывать = Истина;

    выборкаСтатейЗатрат = получитьВыборкуУслугПоСтатьямЗатрат();
    Если выборкаСтатейЗатрат = Неопределено Тогда
        Возврат;
    КонецЕсли;

    Пока выборкаСтатейЗатрат.Следующий() Цикл
        выполнитьДвижениеУчетЗатратОборот(выборкаСтатейЗатрат);

        выборкаУслугПоНоменклатуре = выборкаСтатейЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
        Пока выборкаУслугПоНоменклатуре.Следующий() Цикл
            выполнитьДвижениеБУХозрасчетный(выборкаУслугПоНоменклатуре.СчетБухгалтерскогоУчета,
            выборкаУслугПоНоменклатуре.СтатьяЗатрат, выборкаУслугПоНоменклатуре.Сумма);
        КонецЦикла;
    КонецЦикла;
КонецПроцедуры

Процедура выполнитьДвижениеБУХозрасчетный(Знач счетДт, Знач статьяЗатрат, Знач сумма)
    движение = Движения.Хозрасчетный.Добавить();
    движение.СчетДт = счетДт;
    движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
    движение.Период = ЭтотОбъект.Дата;
    движение.Сумма = сумма;
    движение.Содержание = "Отражено поступление услуг от поставщика";
    БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(движение.СчетДт, движение.СубконтоДт, статьяЗатрат);
    БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(движение.СчетКт, движение.СубконтоКт, ЭтотОбъект.Поставщик);
КонецПроцедуры

Процедура выполнитьДвижениеУчетЗатратОборот(Знач данныеЗатрат)
    движение = Движения.УчетЗатрат.Добавить();
    движение.Период = ЭтотОбъект.Дата;
    движение.СтатьяЗатрат = данныеЗатрат.СтатьяЗатрат;
    движение.Сумма = данныеЗатрат.Сумма;

    ДиагностикаКлиентСервер.Утверждение(движение.Сумма <> Неопределено И движение.СтатьяЗатрат <> Неопределено,
        "Значения строки ""УчетЗатрат"" не заполнены.");
КонецПроцедуры
#КонецОбласти // Движения

// Возвращаемое значение:
//  - ВыборкаИзРезультатаЗапроса, Неопределено
Функция получитьВыборкуУслугПоСтатьямЗатрат()
    результат = Документы.ПоступлениеУслуг.ПолучитьВыборкуУслугПоСтатьямЗатрат(ЭтотОбъект.Ссылка);
    ДиагностикаКлиентСервер.Утверждение(результат <> Неопределено,
        "Ошибка получения выборки услуг документа ""ПоступлениеУслуг"".");

    Возврат результат;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
