#Область ПрограммныйИнтерфейс

// Возвращаемое значение:
//  Массив - массив объектов вида: { ИмяТЧ: Строка, Столбец: Строка } наименований
// 	табличных частей (ТЧ) и столбцов ТЧ используемых для расчета суммы документа.
//	{ ИмяТЧ - Имя ТабличнойЧасти, Столбец - Наименование Реквизита ТабличнойЧасти }
//
Функция ПолучитьПоляДляРасчетаСуммыДокумента() Экспорт
    имяТаблицыТоваров = Метаданные.Документы.ПоступлениеТоваров.ТабличныеЧасти.Товары.Имя;

    коллекцияПолей = Новый Массив(1);
    коллекцияПолей[0] = РаботаСДокументамиКлиентСервер.СоздатьЭлементРасчетаСуммыДокумента(имяТаблицыТоваров, "Сумма");

    Возврат коллекцияПолей;
КонецФункции

// Параметры:
//  документСсылка - ДокументСсылка.ПоступлениеТоваров
//  отражатьСрокиГодности - Булево
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса, Неопределено
Функция ПолучитьВыборкуТовары(Знач документСсылка, Знач отражатьСрокиГодности) Экспорт
    запросТовары = Новый Запрос;
    запросТовары.УстановитьПараметр("Ссылка", документСсылка);

    запросТовары.Текст =
        "ВЫБРАТЬ
        |	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
        |	СУММА(ПоступлениеТоваровТовары.Количество) КАК Количество,
        |	СУММА(ПоступлениеТоваровТовары.Сумма) КАК Сумма,
        |	ПоступлениеТоваровТовары.СрокГодности КАК СрокГодности,
        |   ТаблНоменклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
        |ИЗ
        |	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
        |       ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблНоменклатура
        |       ПО (ТаблНоменклатура.Ссылка = ПоступлениеТоваровТовары.Номенклатура)
        |ГДЕ
        |	ПоступлениеТоваровТовары.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПоступлениеТоваровТовары.Номенклатура,
        |	ПоступлениеТоваровТовары.СрокГодности,
        |   ТаблНоменклатура.СчетБухгалтерскогоУчета
        |";

    Если НЕ отражатьСрокиГодности Тогда // Удаляем поле СрокГодности из запроса
        запросТовары.Текст = СтрЗаменить(запросТовары.Текст, "ПоступлениеТоваровТовары.СрокГодности КАК СрокГодности,", "");
        запросТовары.Текст = СтрЗаменить(запросТовары.Текст, "ПоступлениеТоваровТовары.СрокГодности,", "");
    КонецЕсли;

    результатЗапроса = запросТовары.Выполнить();
    Если результатЗапроса.Пустой() Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат результатЗапроса.Выбрать();
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
