#Область ПрограммныйИнтерфейс

// Устарела.
// Не используется. Обновление суммы документа выполняется в процессе заполнения формы.
//
// Обновляет поле СуммаДокумента.\
// Для корректной рабаты все суммы по строкам ТЧ должны быть предварительно рассчитаны.
Процедура ОбновитьСуммуДокумента() Экспорт
    опцииРасчета = Документы.ПоступлениеТоваров.ПолучитьПоляДляРасчетаСуммыДокумента();
    сумма = РаботаСДокументамиКлиентСервер.РассчитатьСуммуДокумента(ЭтотОбъект, опцииРасчета);
    СуммаДокумента = сумма;
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(_, __)
    выполнитьВсеДвижения();
КонецПроцедуры

Процедура ОбработкаЗаполнения(_, __, ___)
    РаботаСДокументами.ЗаполнитьПолеАвторДокументаНаСервере(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

#Область Движения
Процедура выполнитьВсеДвижения()
    отражатьСрокиГодности = получитьУчетнуюПолитику() = Перечисления.ВидыУчетнойПолитики.FEFO;

    Движения.ТоварыНаСкладах.Записывать = Истина;
    Движения.Хозрасчетный.Записывать = Истина;

    выборкаТоварыДокумента = получитьВыборкуТоварыПоПартиям(отражатьСрокиГодности);
    Если выборкаТоварыДокумента = Неопределено Тогда
        Возврат;
    КонецЕсли;

    Пока выборкаТоварыДокумента.Следующий() Цикл
        выполнитьДвижениеТоварыНаСкладах(выборкаТоварыДокумента, отражатьСрокиГодности);
        выполнитьДвижениеБУХозрасчетный(выборкаТоварыДокумента.Номенклатура,
            выборкаТоварыДокумента.СчетБухгалтерскогоУчета, выборкаТоварыДокумента.Сумма);
    КонецЦикла;
КонецПроцедуры

Процедура выполнитьДвижениеБУХозрасчетный(Знач номенклатураСсылка, Знач счетДт, Знач сумма)
    ДиагностикаКлиентСервер.Утверждение(номенклатураСсылка <> Неопределено И счетДт <> Неопределено И сумма <> Неопределено,
        "Все аргументы должны иметь определенные значения.");
    ДиагностикаКлиентСервер.Утверждение(ТипЗнч(счетДт) = Тип("ПланСчетовСсылка.Хозрасчетный"),
        "Аргумент ""СчетДт"" должен иметь значение типа: ""ПланСчетовСсылка.Хозрасчетный"".");

    движение = Движения.Хозрасчетный.Добавить();
    движение.СчетДт = счетДт;
    движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
    движение.Период = ЭтотОбъект.Дата;
    движение.Сумма = сумма;
    движение.Содержание = "Отражено поступление товарно-материальных ценностей от поставщика";
    движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура] = номенклатураСсылка;
    движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты] = ЭтотОбъект.Поставщик;
КонецПроцедуры

Процедура выполнитьДвижениеТоварыНаСкладах(текСтрокаТовары, Знач отражатьСрокиГодности = Ложь)
    движение = Движения.ТоварыНаСкладах.Добавить();
    движение.ВидДвижения = ВидДвиженияНакопления.Приход;
    движение.Период = ЭтотОбъект.Дата;
    движение.Номенклатура = текСтрокаТовары.Номенклатура;
    движение.Склад = ЭтотОбъект.Склад;
    движение.Количество = текСтрокаТовары.Количество;
    движение.Сумма = текСтрокаТовары.Сумма;
    Если отражатьСрокиГодности Тогда
        движение.СрокГодности = текСтрокаТовары.СрокГодности;
    КонецЕсли;
КонецПроцедуры
#КонецОбласти // Движения

Функция получитьВыборкуТоварыПоПартиям(отражатьСрокиГодности)
    запросТовары = Новый Запрос;
    запросТовары.УстановитьПараметр("Ссылка", Ссылка);

    запросТовары.Текст =
        "ВЫБРАТЬ
        |	ПоступлениеТоваров.Номенклатура КАК Номенклатура,
        |	СУММА(ПоступлениеТоваров.Количество) КАК Количество,
        |	СУММА(ПоступлениеТоваров.Сумма) КАК Сумма,
        |	ПоступлениеТоваров.СрокГодности КАК СрокГодности,
        |   ТаблНоменклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
        |ИЗ
        |	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваров
        |       ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблНоменклатура
        |       ПО (ТаблНоменклатура.Ссылка = ПоступлениеТоваров.Номенклатура)
        |ГДЕ
        |	ПоступлениеТоваров.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	ПоступлениеТоваров.Номенклатура,
        |	ПоступлениеТоваров.СрокГодности,
        |   ТаблНоменклатура.СчетБухгалтерскогоУчета
        |";

    Если НЕ отражатьСрокиГодности Тогда // Удаляем поле СрокГодности из запроса
        запросТовары.Текст = СтрЗаменить(запросТовары.Текст, "ПоступлениеТоваров.СрокГодности КАК СрокГодности,", "");
        запросТовары.Текст = СтрЗаменить(запросТовары.Текст, "ПоступлениеТоваров.СрокГодности,", "");
    КонецЕсли;

    результатЗапроса = запросТовары.Выполнить();
    Если результатЗапроса.Пустой() Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат результатЗапроса.Выбрать();
КонецФункции

Функция получитьУчетнуюПолитику()
    Возврат РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(ЭтотОбъект.Дата).УчетнаяПолитика;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
