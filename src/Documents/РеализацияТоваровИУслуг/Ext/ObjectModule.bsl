#Область ПрограммныйИнтерфейс

// Устарела. В текущей реализации не используется.
// Обновление суммы документа выполняется в процессе заполнения формы.\
// Обновляет поле СуммаДокумента.\
// Для корректной рабаты все суммы по строкам ТЧ должны быть предварительно рассчитаны.\
//
Процедура ОбновитьСуммуДокумента() Экспорт
    опцииРасчета = Документы.РеализацияТоваровИУслуг.ПолучитьПоляДляРасчетаСуммыДокумента();
    сумма = РаботаСДокументамиКлиентСервер.РассчитатьСуммуДокумента(ЭтотОбъект, опцииРасчета);
    ЭтотОбъект.СуммаДокумента = сумма;
КонецПроцедуры

// Параметры:
//  принудительно - Булево - Если Ложь - проверяет только если признак оплаты не установлен ранее.
//  По Умолчанию - Истина
Процедура ОбновитьСтатусОплатыДокумента(Знач принудительно = Истина) Экспорт
    Если принудительно ИЛИ ЭтотОбъект.ПризнакОплаты <> Перечисления.ПризнакиОплаты.ПолностьюОплачен Тогда
        ЭтотОбъект.ПризнакОплаты = Документы.РеализацияТоваровИУслуг.ПроверитьОплатуДокумента(Ссылка).ПризнакОплаты;
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(данныеЗаполнения, __)
    Если ТипЗнч(данныеЗаполнения) = Тип("ДокументСсылка.ЗаписьКлиента") Тогда
        ЭтотОбъект.Клиент = данныеЗаполнения.Клиент;

        Если данныеЗаполнения.УслугаОказана Тогда // Ошибка! Попытка повторной реализации услуг по записи клиента
            сообщениеОшибки = СтрШаблон(
                    "Данная запись клиента ""%1"" от ""%2"" оказана и учтена в документах реализации услуг.
                    |Нельзя создавать документ реализации на основании закрытой или отмененной записи клиента.",
                    ЭтотОбъект.Клиент, данныеЗаполнения.ДатаЗаписи);

            ВызватьИсключение сообщениеОшибки;

        Иначе
            ЭтотОбъект.Сотрудник = данныеЗаполнения.Сотрудник;
            ЭтотОбъект.ДокументОснование = данныеЗаполнения.Ссылка;
            Для Каждого текСтрокаУслуги Из данныеЗаполнения.Услуги Цикл
                новаяСтрока = Услуги.Добавить();
                новаяСтрока.Стоимость = текСтрокаУслуги.Стоимость;
                новаяСтрока.Услуга = текСтрокаУслуги.Услуга;
                ЭтотОбъект.СуммаДокумента =
                    ЭтотОбъект.СуммаДокумента + текСтрокаУслуги.Стоимость;
            КонецЦикла;
        КонецЕсли;
    КонецЕсли;

    РаботаСДокументами.ЗаполнитьПолеАвторДокументаНаСервере(ЭтотОбъект);
КонецПроцедуры

Процедура ОбработкаПроведения(отказ, __)
    очиститьДвиженияДокумента();

    результатВыполненияДвижений = выполнитьВсеДвиженияДокумента();
    Если НЕ результатВыполненияДвижений.Отказ Тогда
        записатьДвижения();
        обновитьСтатусОказанияУслуги();
    Иначе
        отказ = Истина;
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

#Область ЗапросыДанных
// Получает выборку Номенклатуры текущего документа и остатки на складе по товарам
// Параметры:
//  менеджерТаблиц - МенеджерВременныхТаблиц, Неопределено
// Возвращаемое значение:
//  - ВыборкаИзРезультатаЗапроса
Функция получитьВыборкуНоменклатураДокументаИОстатки(Знач менеджерТаблиц)
    менеджерТаблиц = ?(менеджерТаблиц = Неопределено, Новый МенеджерВременныхТаблиц, менеджерТаблиц);

    Возврат Документы.РеализацияТоваровИУслуг.ПолучитьВыборкуНоменклатурыДокументаИОстатки(
        ЭтотОбъект.Ссылка, ЭтотОбъект.Склад, Новый Граница(ЭтотОбъект.МоментВремени()));
КонецФункции
#КонецОбласти // ЗапросыДанных

#Область Движения
Процедура записатьДвижения()
    ЭтотОбъект.Движения.ТоварыНаСкладах.Записывать = Истина;
    ЭтотОбъект.Движения.Продажи.Записывать = Истина;
    ЭтотОбъект.Движения.УчетЗатрат.Записывать = Истина;
    ЭтотОбъект.Движения.ЗаказыКлиентов.Записывать = Истина;
    ЭтотОбъект.Движения.Хозрасчетный.Записывать = Истина;

    ЭтотОбъект.Движения.Записать();
КонецПроцедуры

Процедура очиститьДвиженияДокумента()
    записатьДвижения();
    ЭтотОбъект.Движения.ЗаказыКлиентов.Записывать = Истина;
КонецПроцедуры

Функция выполнитьВсеДвиженияДокумента()
    результатВыполнения = Новый Структура("Отказ", Ложь);

    запретПроведенияПриОтрицательныхОстатках = Константы.ЗапретПроведенияПриОтрицательныхОстатках.Получить();

    менеджерТаблиц = Новый МенеджерВременныхТаблиц;

    блокировка = получитьБлокировкуИзмененияТоварыНаСкладах();
    блокировка.Заблокировать();

    выборкаНоменклатуры = получитьВыборкуНоменклатураДокументаИОстатки(менеджерТаблиц);
    Пока выборкаНоменклатуры.Следующий() Цикл
        этоТовар = выборкаНоменклатуры.ЭтоТовар;
        Если этоТовар Тогда
            количествоОстатков = выборкаНоменклатуры.КоличествоОстаток - выборкаНоменклатуры.КоличествоВДокументе;

            Если количествоОстатков < 0 Тогда
                сообщитьПользователюОПревышенииОстатков(
                    -количествоОстатков,
                    выборкаНоменклатуры.НоменклатураПредставление);

                Если запретПроведенияПриОтрицательныхОстатках Тогда
                    результатВыполнения.Отказ = Истина;
                КонецЕсли;
            КонецЕсли;

            Если результатВыполнения.Отказ Тогда
                Продолжить;
            КонецЕсли;

            стоимостьПартии = выполнитьДвиженияТоваровПоПартиям(
                    выборкаНоменклатуры,
                    запретПроведенияПриОтрицательныхОстатках);

            выполнитьДвижениеУчетЗатратОборот(
                выборкаНоменклатуры,
                стоимостьПартии);
        КонецЕсли;

        Если НЕ результатВыполнения.Отказ Тогда
            выполнитьДвижениеПродажиОборот(выборкаНоменклатуры.Номенклатура, выборкаНоменклатуры.СуммаВДокументе);
        КонецЕсли;
    КонецЦикла;

    Если ЗначениеЗаполнено(ЭтотОбъект.Услуги) И получитьЭтоРеализацияНаОснованииЗаписиКлиента() Тогда
        выполнитьДвижениеЗаказыКлиентовРасход();
        результатКонтроляОстатковПоЗаказу = выполнитьКонтрольОстатковДляЗаказыКлиентов();

        Если результатКонтроляОстатковПоЗаказу.Отказ Тогда
            сообщитьПользователюОПревышенииОстатков(
                результатКонтроляОстатковПоЗаказу.ИнформацияОтказа.ПревышениеОстатка, ,
                результатКонтроляОстатковПоЗаказу.ИнформацияОтказа.ЗаписьКлиентаПредставление);
            результатВыполнения.Отказ = Истина;
        КонецЕсли;
    КонецЕсли;

    Если НЕ результатВыполнения.Отказ Тогда
        выполнитьДвижениеБУХозрасчетныйВыручкаОтПродаж();
    КонецЕсли;

    Возврат результатВыполнения;
КонецФункции

Процедура выполнитьДвиженияБУХозрасчетныйСписаниеПартииТоваров(Знач счетКт, Знач номенклатураСсылка, Знач суммаСписания)
    движение = ЭтотОбъект.Движения.Хозрасчетный.Добавить();
    движение.Период = ЭтотОбъект.Дата;
    движение.Сумма = суммаСписания;
    движение.СчетДт = ПланыСчетов.Хозрасчетный.Продажи;
    движение.СчетКт = счетКт;
    движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура] = номенклатураСсылка;
    движение.Содержание = "Списана себестоимость товарно-материальных ценностей";
КонецПроцедуры

Процедура выполнитьДвижениеБУХозрасчетныйВыручкаОтПродаж()
    движение = ЭтотОбъект.Движения.Хозрасчетный.Добавить();
    движение.Период = ЭтотОбъект.Дата;
    движение.Сумма = ЭтотОбъект.СуммаДокумента;
    движение.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями;
    движение.СчетКт = ПланыСчетов.Хозрасчетный.Продажи;
    БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(движение.СчетДт, движение.СубконтоДт, Клиент);
    движение.Содержание = "Отражена выручка от реализации товаров и услуг";
КонецПроцедуры

Процедура выполнитьДвижениеПродажиОборот(Знач номенклатураСсылка, Знач суммаВДокументе)
    движение = ЭтотОбъект.Движения.Продажи.Добавить();
    движение.Период = ЭтотОбъект.Дата;
    движение.Номенклатура = номенклатураСсылка;
    движение.Сотрудник = ЭтотОбъект.Сотрудник;
    движение.Клиент = ЭтотОбъект.Клиент;
    движение.Сумма = суммаВДокументе;
КонецПроцедуры

// Выполняет списание себестоимости партии товаров
Функция выполнитьДвиженияТоваровПоПартиям(
        Знач выборкаНоменклатуры, Знач запретПроведенияПриОтрицательныхОстатках)

    общаяСтоимостьНоменклатуры = 0;
    несписанныйОстаток = выборкаНоменклатуры.КоличествоВДокументе;

    выборкаПартияНоменклатуры = выборкаНоменклатуры.Выбрать();
    количествоГруппТоваров = выборкаПартияНоменклатуры.Количество();
    Пока выборкаПартияНоменклатуры.Следующий() И несписанныйОстаток > 0 Цикл
        // Выполняем проводку по регистру ТоварыНаСкладах для партии товаров
        результатСписания = выполнитьДвижениеТоварыНаСкладахРасход(
                выборкаПартияНоменклатуры,
                несписанныйОстаток);

        общаяСтоимостьНоменклатуры = общаяСтоимостьНоменклатуры + результатСписания.Сумма;
        несписанныйОстаток = несписанныйОстаток - результатСписания.Количество;

        // Выполняем проводку по регистру бухгалтерии для партии товаров
        выполнитьДвиженияБУХозрасчетныйСписаниеПартииТоваров(выборкаПартияНоменклатуры.СчетБухгалтерскогоУчета,
            выборкаПартияНоменклатуры.Номенклатура, результатСписания.Сумма);

        // Код выполняется при отключенной опции ЗапретПроведенияПриОтрицательныхОстатках
        // и формирует отрицательный остаток по номенклатуре
        Если НЕ запретПроведенияПриОтрицательныхОстатках Тогда
            количествоГруппТоваров = количествоГруппТоваров - 1;
            Если несписанныйОстаток < 0 И количествоГруппТоваров = 0 Тогда
                выполнитьДвижениеТоварыНаСкладахРасход(
                    выборкаПартияНоменклатуры,
                    несписанныйОстаток, Истина);
            КонецЕсли;
        КонецЕсли;
    КонецЦикла;

    Возврат общаяСтоимостьНоменклатуры;
КонецФункции

// Устарела. Необходим рефакторинг
// Параметры:
//  выборкаТоварыПоПартиям - Выборка - выборка товаров на складе с группировкой по сроку годности
//  текущийОстатокВДокументе - Число - Остаток количества Номенклатуры (Товаров) в документе
//  этоОтрицательныйОстаток - Булево - Указывает закончились ли товары на складе
//
// Возвращаемое значение:
//  - Структура - { Сумма: Число - Стоимость выполненного списания по партии Товаров, Количество: Число - Количество выполненного списания }
//
Функция выполнитьДвижениеТоварыНаСкладахРасход(выборкаТоварыПоПартиям,
        Знач текущийОстатокВДокументе, Знач этоОтрицательныйОстаток = Ложь)

    доступноДляСписания = ?(этоОтрицательныйОстаток, текущийОстатокВДокументе,
            Мин(выборкаТоварыПоПартиям.КоличествоОстаток, текущийОстатокВДокументе));

    движение = ЭтотОбъект.Движения.ТоварыНаСкладах.ДобавитьРасход();
    движение.Период = ЭтотОбъект.Дата;
    движение.Номенклатура = выборкаТоварыПоПартиям.Номенклатура;
    движение.Склад = выборкаТоварыПоПартиям.Склад;
    движение.СрокГодности = выборкаТоварыПоПартиям.СрокГодности;
    движение.Количество = доступноДляСписания;
    Если доступноДляСписания = выборкаТоварыПоПартиям.КоличествоОстаток ИЛИ этоОтрицательныйОстаток Тогда //!!! Нужно исправить
        движение.Сумма = выборкаТоварыПоПартиям.СуммаОстаток;
    Иначе
        движение.Сумма = доступноДляСписания / выборкаТоварыПоПартиям.КоличествоОстаток
            * выборкаТоварыПоПартиям.СуммаОстаток;
    КонецЕсли;

    результатСписания = Новый Структура("Сумма, Количество", движение.Сумма, движение.Количество);

    Возврат результатСписания;
КонецФункции

Процедура выполнитьДвижениеУчетЗатратОборот(Знач выборкаНоменклатураДокумента, Знач стоимостьОбщая)
    движение = ЭтотОбъект.Движения.УчетЗатрат.Добавить();
    движение.Период = ЭтотОбъект.Дата;
    движение.СтатьяЗатрат = выборкаНоменклатураДокумента.СтатьяЗатрат;
    движение.Сумма = стоимостьОбщая;
КонецПроцедуры

Процедура выполнитьДвижениеЗаказыКлиентовРасход()
    ожидаемыйТипДокумента = Тип("ДокументСсылка.ЗаписьКлиента");
    ДиагностикаКлиентСервер.Утверждение(
        ЗначениеЗаполнено(ЭтотОбъект.ДокументОснование)
        И ТипЗнч(ЭтотОбъект.ДокументОснование) = ожидаемыйТипДокумента,
            СтрШаблон("ДокументОснование должен быть заполнен и иметь тип значения ""%1"".",
                Строка(ожидаемыйТипДокумента)));

    движение = ЭтотОбъект.Движения.ЗаказыКлиентов.Добавить();
    движение.ВидДвижения = ВидДвиженияНакопления.Расход;

    движение.Период = ЭтотОбъект.Дата;
    движение.Клиент = ЭтотОбъект.Клиент;
    движение.ЗаписьКлиента = ЭтотОбъект.ДокументОснование;
    движение.Сумма = ЭтотОбъект.Услуги.Итог("Стоимость");
КонецПроцедуры
#КонецОбласти // Движения

// Выполняет проверку достаточности остатков по Номенклатуре.\
// В случае отсутствия достаточного количества остатков - оповещает Пользователя.
//
// Параметры:
//	количествоВДокументе - Число - Количество Номенклатуры в документе для движения
//	остаток - Число - Остаток Номенклатуры на складах
//	наименованиеНоменклатуры - Строка - Наименование Номенклатуры, используется в тексте сообщения
//
// Возвращаемое значение:
//	Булево - Истина если остатков нехватает, иначе Ложь
//
Функция проверитьПревышениеОстатков(Знач количествоВДокументе, Знач остаток, Знач наименованиеНоменклатуры)
    превышениеОстатковНоменклатуры = количествоВДокументе - остаток;
    Если превышениеОстатковНоменклатуры > 0 Тогда
        сообщение = Новый СообщениеПользователю;
        сообщение.Текст = СтрШаблон("Превышение остатка по номенклатуре: ""%1"" в количестве: ""%2""",
                наименованиеНоменклатуры, превышениеОстатковНоменклатуры);
        сообщение.Сообщить();
        Возврат Истина;
    КонецЕсли;
    Возврат Ложь;
КонецФункции

// Возвращаемое значение:
//	- ФиксированнаяСтруктура - Структура вида { Отказ: Булево, ИнформацияОтказа: ФиксированнаяСтруктура | Неопределено}
Функция выполнитьКонтрольОстатковДляЗаказыКлиентов()
    ЭтотОбъект.Движения.ЗаказыКлиентов.БлокироватьДляИзменения = Истина;
    ЭтотОбъект.Движения.ЗаказыКлиентов.Записать();

    ДиагностикаКлиентСервер.Утверждение(ЭтотОбъект.ДокументОснование <> Неопределено
        И ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.ЗаписьКлиента"),
            "Ошибка! ДокументОснование должен иметь значение типа ДокументСсылка.ЗаписьКлиента.");

    остатокПоЗаказу = Документы.РеализацияТоваровИУслуг.ПолучитьОстатокПоЗаписиКлиента(
            ЭтотОбъект.ДокументОснование, Новый Граница(ЭтотОбъект.МоментВремени()));

    отказ = остатокПоЗаказу <> Неопределено И остатокПоЗаказу.Сумма < 0;
    информацияОтказа = Неопределено;
    Если отказ Тогда
        информацияОтказа = Новый ФиксированнаяСтруктура("ПревышениеОстатка, ЗаписьКлиентаПредставление",
                -остатокПоЗаказу.Сумма, остатокПоЗаказу.ЗаписьКлиентаПредставление);
    КонецЕсли;

    Возврат Новый ФиксированнаяСтруктура("Отказ, ИнформацияОтказа", отказ, информацияОтказа);
КонецФункции

Функция получитьБлокировкуИзмененияТоварыНаСкладах() // => БлокировкаДанных
    блокировка = Новый БлокировкаДанных;
    элементБлокировки = блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
    элементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
    элементБлокировки.ИсточникДанных = ЭтотОбъект.Товары;
    элементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Товар");
    элементБлокировки.УстановитьЗначение("Склад", ЭтотОбъект.Склад);

    Возврат блокировка;
КонецФункции

Функция получитьЭтоРеализацияНаОснованииЗаписиКлиента()
    Возврат ЗначениеЗаполнено(ЭтотОбъект.ДокументОснование) И ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.ЗаписьКлиента");
КонецФункции

Функция обновитьСтатусОказанияУслуги()
    Если получитьЭтоРеализацияНаОснованииЗаписиКлиента() Тогда
        записьКлиентаОбъект = ЭтотОбъект.ДокументОснование.ПолучитьОбъект();
        Если НЕ записьКлиентаОбъект.Заблокирован() Тогда
            записьКлиентаОбъект.ОбновитьСтатусОказанияУслуги();
            Если записьКлиентаОбъект.Модифицированность() Тогда
                записьКлиентаОбъект.Записать(РежимЗаписиДокумента.Запись);
            КонецЕсли;
        КонецЕсли;
    КонецЕсли;

    Возврат Ложь;
КонецФункции

// Параметры:
//	превышениеОстатков - Число - Количество превышения остатков
//	наименованиеНоменклатуры - Строка, Неопределено - Наименование Номенклатуры, используется в тексте сообщения
//	записьКлиентаПредставление - Строка, Неопределено - Строковое представление записи клиента
Процедура сообщитьПользователюОПревышенииОстатков(
        Знач превышениеОстатков, Знач наименованиеНоменклатуры = Неопределено, Знач записьКлиентаПредставление = Неопределено)
    ДиагностикаКлиентСервер.Утверждение(наименованиеНоменклатуры <> Неопределено
        ИЛИ записьКлиентаПредставление <> Неопределено,
        "Один из необязательных аргументов: [НаименованиеНоменклатуры, ЗаписьКлиентаПредставление] должен быть заполнен.");

    форматФинансовыхДанных = "ЧДЦ=2; ЧРГ= ; ЧН=0.00";
    сообщение = Новый СообщениеПользователю;
    текстСообщения = "";
    Если записьКлиентаПредставление <> Неопределено Тогда // Это превышение суммы по записи клиента
        текстСообщения = СтрШаблон("Превышена сумма по записи клиента ""%1"" на ""%2""руб.",
                записьКлиентаПредставление, Формат(превышениеОстатков, форматФинансовыхДанных));
    Иначе
        текстСообщения = СтрШаблон("Превышение остатка по номенклатуре: ""%1"" в количестве: ""%2""",
                наименованиеНоменклатуры, превышениеОстатков);
    КонецЕсли;

    сообщение.Текст = текстСообщения;
    сообщение.Сообщить();
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
