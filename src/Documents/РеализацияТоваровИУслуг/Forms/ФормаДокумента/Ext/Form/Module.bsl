#Область ОписаниеПеременных

// Хранит названия табличных частей и наименования столбцов необходимые
// для расчета суммы документа
&НаКлиенте
Перем _ПоляРасчетаСуммыДокумента;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(_)
    _ПоляРасчетаСуммыДокумента = получитьПоляИтоговНаСервереБезКонтекста();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(_)
    текущаяСтрокаТовары_ = Элементы.Товары.ТекущиеДанные;
    обновитьСуммуДокумента(текущаяСтрокаТовары_);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыТоварПриИзменении(_)
    текущаяСтрокаТовары_ = Элементы.Товары.ТекущиеДанные;
    текущаяСтрокаТовары_.Цена = получитьЦенуПродажи(текущаяСтрокаТовары_.Товар);

    обновитьСуммуДокумента(текущаяСтрокаТовары_);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(_)
	обновитьСуммуДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыТовары

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиУслугаПриИзменении(_)
    текущаяСтрокаУслуги_ = Элементы.Услуги.ТекущиеДанные;
    текущаяСтрокаУслуги_.Стоимость = получитьЦенуПродажи(текущаяСтрокаУслуги_.Услуга);

    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура УслугиПослеУдаления(_)
	обновитьСуммуДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыУслуги

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция рассчитатьСуммуПоСтрокеТовары(текущСтрокаТовары)
    Возврат текущСтрокаТовары.Цена * текущСтрокаТовары.Количество;
КонецФункции

&НаКлиенте
Функция рассчитатьСуммуДокумента()
    Возврат РаботаСДокументамиКлиентСервер.РассчитатьСуммуДокумента(Объект, _ПоляРасчетаСуммыДокумента);
КонецФункции

&НаКлиенте
Процедура обновитьСуммуДокумента(текущаяСтрокаТовары = Неопределено)
    Если текущаяСтрокаТовары <> Неопределено Тогда
        текущаяСтрокаТовары.Сумма = рассчитатьСуммуПоСтрокеТовары(текущаяСтрокаТовары);
    КонецЕсли;

    Объект.СуммаДокумента = рассчитатьСуммуДокумента();
КонецПроцедуры

&НаСервереБезКонтекста
Функция получитьПоляИтоговНаСервереБезКонтекста()
    Возврат Документы.РеализацияТоваровИУслуг.ПолучитьПоляДляРасчетаСуммыДокумента();
КонецФункции

&НаКлиенте
Функция получитьЦенуПродажи(Знач номенклатура)
    цена_ = РаботаСЦенамиВызовСервера.ПолучитьЦенуПродажиНаДату(номенклатура);

    Если цена_ = Неопределено Тогда
        текстСообщения_ = СтрШаблон("Не удалось получить текущую цену продажи для номенклатуры: ""%1"".\n
                |Проверьте наличие цены для номенклатуры в регистре цен",
                номенклатура);
        ПоказатьПредупреждение( , текстСообщения_, "Ошибка!");
        цена_ = 0;
    КонецЕсли;

    Возврат цена_;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
