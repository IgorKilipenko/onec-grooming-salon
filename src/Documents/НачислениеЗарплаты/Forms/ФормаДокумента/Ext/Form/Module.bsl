#Область ОписаниеПеременных

&НаКлиенте
Перем _ПланВидовРасчетаОклад;

#КонецОбласти // ОписаниеПеременных

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(_)
    инициализация();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(_)
    Объект.Начисления.Очистить();
    началоПериода = НачалоМесяца(Объект.ПериодНачисления);
    конецПериода = КонецМесяца(Объект.ПериодНачисления);

    кадроваяИсторияСотрудников = получитьКадровыеДанныеПоВсемСотрудникамНаСервере(началоПериода, конецПериода);
    Если кадроваяИсторияСотрудников = Неопределено Тогда
        сообщение = Новый СообщениеПользователю;
        сообщение.Текст = "Кадровая история за период начисления отсутствует.";
        сообщение.Сообщить();

        Возврат;    // Выход
    КонецЕсли;

    Для Каждого данныеСотрудника Из кадроваяИсторияСотрудников Цикл
        строкаНачисления = Объект.Начисления.Добавить();

        строкаНачисления.ВидРасчета = _ПланВидовРасчетаОклад;
        строкаНачисления.Сотрудник = данныеСотрудника.Сотрудник;
        строкаНачисления.ГрафикРаботы = данныеСотрудника.ГрафикРаботы;
        строкаНачисления.ДатаНачала = Макс(началоПериода, данныеСотрудника.Период);
        строкаНачисления.ДатаОкончания = Мин(конецПериода, КонецМесяца(данныеСотрудника.Период));
        строкаНачисления.ПоказательРасчета = данныеСотрудника.Оклад;
    КонецЦикла;
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция получитьКадровыеДанныеПоВсемСотрудникамНаСервере(Знач началоПериода, Знач конецПериода)
    параметрыЗапроса = Новый Структура("НачалоПериода, КонецПериода",
            началоПериода, конецПериода);

    кадроваяИсторияСотрудников = РегистрыСведений.КадроваяИсторияСотрудников.ПолучитьИсториюЗаПериод(
            Неопределено, параметрыЗапроса, Истина);

    результат = РаботаСКоллекциями.КонвертироватьТаблицуВМассив(кадроваяИсторияСотрудников);

    Возврат результат;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции

#Область Инициализация

&НаКлиенте
Процедура инициализация()
    _ПланВидовРасчетаОклад = получитьПланВидовРасчетаОкладНаСервере();
КонецПроцедуры

&НаСервереБезКонтекста
Функция получитьПланВидовРасчетаОкладНаСервере()
    Возврат ПланыВидовРасчета.Начисления.Оклад;
КонецФункции

#КонецОбласти // Инициализация
