#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПередЗаписью(_, __)
    Объект.Наименование = составитьФио(Объект.Фамилия, Объект.Имя, Объект.Отчество);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФамилияПриИзменении(редактируемыйЭлементФормы)
    заполнитьПолеФормыФио(редактируемыйЭлементФормы);
КонецПроцедуры

&НаКлиенте
Процедура ИмяПриИзменении(редактируемыйЭлементФормы)
    заполнитьПолеФормыФио(редактируемыйЭлементФормы);
КонецПроцедуры

&НаКлиенте
Процедура ОтчествоПриИзменении(редактируемыйЭлементФормы)
    заполнитьПолеФормыФио(редактируемыйЭлементФормы);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура заполнитьПолеФормыФио(редактируемыйЭлементФормы)
    текстРедактирования = редактируемыйЭлементФормы.ТекстРедактирования;

    Попытка
        Если редактируемыйЭлементФормы.Имя = "Фамилия" Тогда
            Объект.Фамилия = очиститьФиоПоМаске(текстРедактирования);
        ИначеЕсли редактируемыйЭлементФормы.Имя = "Имя" Тогда
            Объект.Имя = очиститьФиоПоМаске(текстРедактирования);
        ИначеЕсли редактируемыйЭлементФормы.Имя = "Отчество" Тогда
            Объект.Отчество = очиститьФиоПоМаске(текстРедактирования);
        КонецЕсли;

        Объект.Наименование = составитьФио(Объект.Фамилия, Объект.Имя, Объект.Отчество);
    Исключение
        сообщение = Новый СообщениеПользователю();
        сообщение.Текст = СтрШаблон("Неверный формат ввода для поля ""%1"".", редактируемыйЭлементФормы.Имя);
        сообщение.Поле = редактируемыйЭлементФормы.Имя;
        сообщение.КлючДанных = Объект.Ссылка;
        сообщение.ПутьКДанным = "Объект";
        сообщение.Сообщить();
    КонецПопытки;
КонецПроцедуры

&НаСервере
Функция очиститьФиоПоМаске(Знач стрФио)
    стрФио = СтрЗаменитьПоРегулярномуВыражению(стрФио, "\s+", "");
    результатПоиска = СтрНайтиПоРегулярномуВыражению(
            стрФио, "^(?:[^а-яА-ЯЁё]+)?([а-яА-ЯЁё]+)((?:\-[а-яА-ЯЁё]+)?)(?:[^а-яА-ЯЁё]+)?$");

    Если результатПоиска.НачальнаяПозиция > 0 Тогда
        группыПоиска = результатПоиска.ПолучитьГруппы();
        Если ЗначениеЗаполнено(группыПоиска)
            И группыПоиска[0].НачальнаяПозиция > 0 И группыПоиска.Количество() >= 1 Тогда

            результат = группыПоиска[0].Значение;
            результат = ВРег(Лев(результат, 1)) + НРег(Сред(результат, 2));

            группыПоиска.Удалить(0);
            Для Каждого группа Из группыПоиска Цикл
                Если группа.НачальнаяПозиция > 0 Тогда
                    результат = результат + ВРег(Лев(группа.Значение, 2)) + НРег(Сред(группа.Значение, 3));
                КонецЕсли;
            КонецЦикла;

            Возврат результат;
        КонецЕсли;
    Иначе
        ВызватьИсключение("Ошибка. Неверный формат ввода.");
    КонецЕсли;
КонецФункции

&НаКлиенте
Функция составитьФио(Знач фамилия, Знач имя, Знач отчество)
    Возврат СокрЛП(СтрШаблон("%1 %2 %3", фамилия, имя, отчество));
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
