#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(_)
    установитьВидимостьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(отказ, __)
    заполнитьПолеНаименованияДоговора();
    Если этоБессрочныйДоговор() И ЗначениеЗаполнено(Объект.ДатаОкончанияДоговора) Тогда
        Объект.ДатаОкончанияДоговора = NULL;
    Иначе
        сообщение = проверкаЗаполненияДатыОкончанияДоговора();
        Если сообщение <> NULL Тогда
            сообщение.Сообщить();
            отказ = Истина;
        КонецЕсли;
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПризнакБессрочногоДоговораПриИзменении(_)
    установитьВидимостьЭлементовФормы();

    Если ЗначениеЗаполнено(Объект.ВнешнийНомерДоговора) И ЗначениеЗаполнено(Объект.ДатаЗаключенияДоговора) Тогда
        заполнитьПолеНаименованияДоговора();
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область СлужебныеПроцедурыИФункции

// Устанавливает/отключает видимость для полей формы заполняемых в зависимости
// от признака бессрочности договора
&НаКлиенте
Процедура установитьВидимостьЭлементовФормы()
    этоСрочныйДоговор = НЕ этоБессрочныйДоговор();
    Элементы.ДатаОкончанияДоговора.Видимость = этоСрочныйДоговор;
    Элементы.ДатаОкончанияДоговора.АвтоОтметкаНезаполненного = этоСрочныйДоговор;
    Объект.ДатаОкончанияДоговора = ?(этоСрочныйДоговор, Объект.ДатаОкончанияДоговора, NULL);
КонецПроцедуры

// Заполняет поле формы - НаименованиеДоговора по указанным в форме данным номера и даты договора
&НаКлиенте
Процедура заполнитьПолеНаименованияДоговора()
    Объект.Наименование = сформироватьСтрокуНаименованияДоговора(Объект.ВнешнийНомерДоговора,
            Объект.ДатаЗаключенияДоговора);
КонецПроцедуры

// Формирует строку наименования договора / вида: №договора от датаДоговора.
//
// Параметры:
//  номерДоговора - Строка - Номер договора
//  датаЗаключения - Дата - Дата заключения договора
//
// Возвращаемое значение:
//  Строка - наименования договора (прим. №123 от 12.05.2022 г)
&НаКлиенте
Функция сформироватьСтрокуНаименованияДоговора(Знач номерДоговора, Знач датаЗаключения)
    номерДоговора = РаботаСоСтркамиВызовСервера.ЗаменитьПоРегулярномуВыражению(
            номерДоговора, "^\s+|\s+$", "");
    номерДоговора = РаботаСоСтркамиВызовСервера.ЗаменитьПоРегулярномуВыражению(
            номерДоговора, "^\D+(?=[0-9]+)", "");

    Возврат СтрШаблон("№%1 от %2 г.", номерДоговора, Формат(датаЗаключения, "Л=ru_RU; ДФ=дд.MM.гггг"));
КонецФункции

&НаКлиенте
Функция этоБессрочныйДоговор()
    Возврат Объект.ПризнакБессрочногоДоговора;
КонецФункции

&НаКлиенте
Функция получитьПоляФормыТолькоДляСрочногоДоговора()
    поляФормы = Новый Массив();
    поляФормы.Добавить(Элементы.ДатаОкончанияДоговора);

    Возврат поляФормы;
КонецФункции

// Выполняет проверку заполнения поля ДатаОкончанияДоговора. \
// Если поле ДатаОкончанияДоговора не прошло проверку - генерирует сообщение пользователю
//
// Параметры:
//   принудительно - Булево - Если Ложь проверка выполняется только для объекта с признаком БессрочностиДоговора
//
// Возвращаемое значение:
//   СообщениеПользователю - Если NULL - проверка прошла успешно, иначе возвращает сообщение с текстом ошибки проверки
&НаКлиенте
Функция проверкаЗаполненияДатыОкончанияДоговора(Знач принудительно = Ложь)
    этоСрочныйДоговор = НЕ этоБессрочныйДоговор();

    Если (принудительно ИЛИ этоСрочныйДоговор) И (НЕ ЗначениеЗаполнено(Объект.ДатаОкончанияДоговора)) Тогда
        сообщение = Новый СообщениеПользователю();
        сообщение.Текст = "Поле ""дата окончания договора"" не заполнено";
        сообщение.Поле = Элементы.ДатаОкончанияДоговора.Имя;
        Сообщение.КлючДанных = Объект.Ссылка;
        Сообщение.ПутьКДанным = "Объект";

        Возврат сообщение;
    КонецЕсли;

    Возврат NULL;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
